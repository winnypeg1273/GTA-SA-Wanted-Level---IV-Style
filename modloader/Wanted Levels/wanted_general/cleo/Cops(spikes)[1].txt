
{$CLEO .cs}

thread 'POLICESPIKES'

:POLICESPIKES_1
wait 1000 
if /*and
   $ONMISSION == 0*/  
   Player.Defined($PLAYER_CHAR) 
jf @POLICESPIKES_1
if 
   Player.WantedLevel($PLAYER_CHAR) > 2
jf @POLICESPIKES_1

:POLICESPIKES_2
if
    0154:   actor $PLAYER_ACTOR in_zone 'VE'  // Las Venturas
jf @POLICESPIKES_3 
15@ = #LVPD1 
jump @POLICESPIKES_7

:POLICESPIKES_3
if
    0154:   actor $PLAYER_ACTOR in_zone 'LA'  // Los Santos
jf @POLICESPIKES_4 
15@ = #LAPD1 
jump @POLICESPIKES_7

:POLICESPIKES_4
if
    0154:   actor $PLAYER_ACTOR in_zone 'SF' // San Fierro
jf @POLICESPIKES_1 
15@ = #SFPD1 

:POLICESPIKES_7
IF 
   Actor.Driving($PLAYER_ACTOR)
jf @POLICESPIKES_1
if or
   Actor.DrivingVehicleType($PLAYER_ACTOR, #BMX)
   Actor.DrivingVehicleType($PLAYER_ACTOR, #BIKE)
   Actor.DrivingVehicleType($PLAYER_ACTOR, #RHINO)
   04C8:   actor $PLAYER_ACTOR driving_flying_vehicle
   04A7:   actor $PLAYER_ACTOR driving_boat
jf @POLICESPIKES_12
JUMP @POLICESPIKES_1

:POLICESPIKES_12   
03C0: 0@ = actor $PLAYER_ACTOR car 
0407: store_coords_to 1@ 2@ 3@ from_car 0@ with_offset 0.0 100.0 0.0
02C0: store_to 4@ 5@ 6@ ped_path_coords_closest_to 1@ 2@ 3@
if
    0100: actor $PLAYER_ACTOR in_sphere 4@ 5@ 6@ radius 100.0 100.0 100.0 sphere 0 in_car
jf @POLICESPIKES_1                        
if and
    0496: tire 0 on_car 0@ deflated
    0496: tire 1 on_car 0@ deflated
    0496: tire 2 on_car 0@ deflated
    0496: tire 3 on_car 0@ deflated
jf @POLICESPIKES_41
jump @POLICESPIKES_1


:POLICESPIKES_41
if or
   not Model.Available(15@)  
   not Model.Available(#SILENCED)
   not Model.Available(2892)   
jf @POLICESPIKES_51 
Model.Load(15@)
Model.Load(#SILENCED)
Model.Load(2892)
wait 500
jump @POLICESPIKES_1

:POLICESPIKES_51   //create actors
Actor.Create(14@, 29, 15@, 4@, 5@, 6@)
Actor.GiveWeaponAndAmmo(14@, 23, 1000)
4@ = 0.0
5@ = 0.0
6@ = 0.0

:POLICESPIKES_51b //create stinger
04C4: store_coords_to 17@ 18@ 19@ from_actor 14@ with_offset 0.0 0.0 2.0
02CE: 19@ = ground_z_at 17@ 18@ 19@ 
03D3: get_route_nearest_for 17@ 18@ 19@ store_to 17@ 18@ 19@ Z_angle_to 21@
//02C1: store_to 17@ 18@ 19@ car_path_coords_closest_to 17@ 18@ 19@ 
19@ += 0.01
Object.Create(10@, 2892, 17@, 18@, 19@)
0382: set_object 10@ collision_detection 0
09CA: set_object 10@ immunities BP 0 FP 0 EP 0 CP 1 MP 1
//21@ = Actor.Angle($PLAYER_ACTOR)
21@ += 90.0
0453: object 10@ set_rotation 0.0 0.0 21@

:POLICE_SPIKES_51c  //set task
0400: store_coords_to 4@ 5@ 6@ from_object 10@ with_offset 0.0 -5.0 0.0
Actor.PutAt(14@, 4@, 5@, 6@)
0350: set_actor 14@ maintain_position_when_attacked 1 
07DD: set_actor 14@ attack_rate 70 // previously known as temper_to
099F: AS_actor 14@ ignore_weapon_range 1
06E4: AS_actor 14@ attempt_to_bust_actor $PLAYER_ACTOR
//05E2: AS_actor 14@ kill_actor $PLAYER_ACTOR 
16@ = 2

:POLICESPIKES_52
wait 0
if and 
   Player.Defined($PLAYER_CHAR)
   Player.WantedLevel($PLAYER_CHAR) > 1
   //$ONMISSION == 0
jf @POLICESPIKES_6001
0@ = 0
 
:POLICESPIKES_81
IF
03CA:   object 10@ exists 
jf @POLICESPIKES_6001
if
0471:   actor $PLAYER_ACTOR near_object_in_rectangle 10@ radius 150.0 150.0 flag 0
jf @POLICESPIKES_6001
0400: store_coords_to 23@ 24@ 25@ from_object 10@ with_offset -0.5 5.0 0.0 
0400: store_coords_to 26@ 27@ 28@ from_object 10@ with_offset -0.5 -5.0 0.0 
if
01A7:   actor $PLAYER_ACTOR sphere 0 in_cube_cornerA 23@ 24@ 25@ cornerB 26@ 27@ 28@ in_car
jf @POLICESPIKES_85
03C0: 0@ = actor $PLAYER_ACTOR car
jump @POLICESPIKES_121

:POLICESPIKES_85
0400: store_coords_to 7@ 8@ 9@ from_object 10@ with_offset 0.0 0.0 0.0 
get_random_car_in_sphere_no_save_recursive 0@ 7@ 8@ 9@ 3.0 0 1
if
056E:   car 0@ defined  
jf @POLICESPIKES_52

:POLICESPIKES_121
053F: set_car 0@ tires_vulnerability 1
04FE: deflate_tire 0 on_car 0@ 
04FE: deflate_tire 1 on_car 0@ 
04FE: deflate_tire 2 on_car 0@ 
04FE: deflate_tire 3 on_car 0@
Car.RemoveReferences(0@)
   
:POLICESPIKES_5999
IF
03CA:   object 10@ exists 
jf @POLICESPIKES_6001
IF
0471:   actor $PLAYER_ACTOR near_object_in_rectangle 10@ radius 150.0 150.0 flag 0
jf @POLICESPIKES_6001
JUMP @POLICESPIKES_52

:POLICESPIKES_6001  
Object.Destroy(10@)
048F: actor 14@ remove_weapons
ACTOR.RemoveReferences(14@)
Model.Destroy(15@)
Model.Destroy(2892)
0209: 16@ = random_int_in_ranges 10000 15000 
wait 16@
jump @POLICESPIKES_1